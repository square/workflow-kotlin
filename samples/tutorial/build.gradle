import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
  ext {
    kotlin_version = '1.8.10'
    workflow_version = "1.12.1-beta04"

    deps = [
        activityktx: 'androidx.activity:activity-ktx:1.3.0',
        agp: "com.android.tools.build:gradle:8.1.2",
        appcompat: 'androidx.appcompat:appcompat:1.3.1',
        constraintlayout: 'androidx.constraintlayout:constraintlayout:2.0.1',
        kotlin: [
            plugin: "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version",
            stdlib: "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version",
            test: "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version",
        ],
        material: 'com.google.android.material:material:1.11.0',
        recyclerview: 'androidx.recyclerview:recyclerview:1.3.2',
        viewbinding: 'androidx.databinding:viewbinding:4.0.1',
        viewmodelktx: 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.0',
        viewmodelsavedstate: 'androidx.lifecycle:lifecycle-viewmodel-savedstate:1.1.0',
        workflow: [
            core_android: "com.squareup.workflow1:workflow-ui-core-android:$workflow_version",
            container_android: "com.squareup.workflow1:workflow-ui-container-android:$workflow_version",
            testing: "com.squareup.workflow1:workflow-testing-jvm:$workflow_version",
        ],
    ]
  }

  repositories {
    google()
    mavenCentral()
  }
  dependencies {
    classpath deps.agp
    classpath deps.kotlin.plugin

    // NOTE: Do not place your application dependencies here; they belong
    // in the individual module build.gradle files
  }
}

subprojects {
  repositories {
    mavenLocal()
    google()
    mavenCentral()
  }

  tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
      freeCompilerArgs += '-opt-in=kotlin.RequiresOptIn'
    }
  }

  plugins.withId("org.jetbrains.kotlin.android")  {
    java {
      toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
      }
    }
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}
