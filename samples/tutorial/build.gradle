import org.jetbrains.kotlin.gradle.tasks.KotlinCompile
// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
  def rootProps = new Properties()
  def rootPropsFile = file("../../gradle.properties")
  if (rootPropsFile.exists()) {
    rootPropsFile.withInputStream { rootProps.load(it) }
  }

  ext {
    kotlin_version = '2.3.10'
    // Use the last published version for dependencies (workflow-core, workflow-testing, etc.)
    // so CI can resolve them from Maven Central. The ai-context plugin resolves via
    // includeBuild in settings.gradle regardless of the version string here.
    workflow_version = rootProps.getProperty("LAST_PUBLISHED_VERSION_NAME") ?: "1.25.1"

    deps = [
        activityktx: 'androidx.activity:activity-ktx:1.8.2',
        agp: "com.android.tools.build:gradle:8.11.1",
        appcompat: 'androidx.appcompat:appcompat:1.7.0',
        constraintlayout: 'androidx.constraintlayout:constraintlayout:2.1.4',
        kotlin: [
            plugin: "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version",
            stdlib: "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version",
            test: "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version",
        ],
        material: 'com.google.android.material:material:1.11.0',
        recyclerview: 'androidx.recyclerview:recyclerview:1.3.2',
        viewbinding: 'androidx.databinding:viewbinding:8.1.2',
        viewmodelktx: 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.7',
        viewmodelsavedstate: 'androidx.lifecycle:lifecycle-viewmodel-savedstate:2.8.7',
        workflow: [
            core_android: "com.squareup.workflow1:workflow-ui-core-android:$workflow_version",
            testing: "com.squareup.workflow1:workflow-testing-jvm:$workflow_version",
        ],
    ]
  }

  repositories {
    google()
    mavenCentral()
  }
  dependencies {
    classpath deps.agp
    classpath deps.kotlin.plugin

    // NOTE: Do not place your application dependencies here; they belong
    // in the individual module build.gradle files
  }
}

plugins {
  id 'com.squareup.workflow1.ai-context' version "$workflow_version"
}

subprojects {
  repositories {
    mavenLocal()
    google()
    mavenCentral()
  }

  tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
      freeCompilerArgs += '-opt-in=kotlin.RequiresOptIn'
    }
  }

  plugins.withId("org.jetbrains.kotlin.android")  {
    java {
      toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
      }
    }
  }
}

tasks.register("clean", Delete) {
  delete rootProject.buildDir
}

tasks.register("verifyAiRulesExtraction") {
  dependsOn 'extractAiContext'
  group = 'verification'
  description = 'Verifies that AI rules and skills were correctly extracted from workflow JARs'

  doLast {
    // The ai-context plugin defaults to the `amp` tool, which extracts skills to `.agents/skills`.
    // These are the minimum expected skills from workflow-core packaging.
    def expectedSkillNames = [
      'create-workflow',
      'workflow-testing',
      'workflow-integration-testing',
      'extracting-ai-context',
    ]
    def expectedSkills = expectedSkillNames.collect { skillName ->
      ".agents/skills/${skillName}/SKILL.md"
    }
    def expectedAgents = [
      'AGENTS.md',
    ]

    def missing = []

    (expectedSkills + expectedAgents).each { path ->
      def f = file(path)
      if (!f.exists()) {
        missing << path
      } else if (f.text.trim().isEmpty()) {
        missing << "$path (empty)"
      }
    }

    if (!missing.isEmpty()) {
      throw new GradleException(
        "AI rules extraction verification failed. Missing or empty files:\n" +
        missing.collect { "  - $it" }.join('\n')
      )
    }

    println "AI rules extraction verification passed!"
    println "  Skills: ${expectedSkills.size} verified"
  }
}
